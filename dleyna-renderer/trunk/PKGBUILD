# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-renderer
pkgver=0.7.1
pkgrel=1
pkgdesc="Library to discover and manipulate Digital Media Renderers"
url="https://github.com/phako/dleyna-renderer"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git meson docbook-xsl)
_commit=1c150810f7c307f1a8cdeea530bfeb97fd7f5d03  # tags/v0.7.1^0
source=("git+https://github.com/phako/dleyna-renderer#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix parsing of empty DIDL response
  git cherry-pick -n 92b53e1ba868c717227bdb60e9754ad37dae5cd0
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
