# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=4.16.1
pkgrel=2
pkgdesc="Application library for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'hicolor-icon-theme')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        cell_area-init.patch)
sha256sums=('528dac256315ffc2a4a53b3b421979327962121989886e3cf920aeff9912b53b'
            '648ecddc68c2de8f3cc1474356ba99a4bcd27b45273338655b604272d4bfbe44')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://gitlab.xfce.org/xfce/exo/-/issues/57#note_29559
  patch -Np1 -i ../cell_area-init.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
