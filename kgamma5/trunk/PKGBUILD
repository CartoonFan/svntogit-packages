# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kgamma5
pkgver=5.22.1
pkgrel=1
pkgdesc='Adjust your monitor gamma settings'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(systemsettings)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('43413d51c56b6328f90cad23ea7f34a6c04cbb22382421d3fb443d0baa85f083'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
