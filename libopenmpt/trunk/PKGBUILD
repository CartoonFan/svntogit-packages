# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libopenmpt
pkgver=0.5.13
pkgrel=1
pkgdesc="A library to render tracker music to a PCM audio stream"
arch=('x86_64')
url="https://lib.openmpt.org/libopenmpt/"
license=('BSD')
depends=(
  flac libFLAC.so
  gcc-libs
  glibc
  libogg libogg.so
  libpulse libpulse.so libpulse-simple.so
  libsndfile libsndfile.so
  libvorbis libvorbis.so libvorbisfile.so
  mpg123 libmpg123.so
  portaudio libportaudio.so
)
makedepends=(autoconf-archive doxygen help2man)
# TODO: remove provides/replaces on openmpt123 after 2022-05-01
provides=(libopenmpt.so openmpt123)
replaces=(openmpt123)
source=(
  "$pkgname-$pkgver.tar.gz::https://lib.openmpt.org/files/libopenmpt/src/${pkgname}-${pkgver}+release.autotools.tar.gz"
)
sha512sums=('5ddf293755ca9696acaeaf2130a38dece3bba9a45519e1d4b61b594b2e014e67e6f3f79f55d54e7cc2528b9e40feac6d36bbe78788b6f2a1888f89c090872242')
b2sums=('dd8abffb89950e14843a19d8f8066b052a773a1ca8f441c5fd02b191886d605db693dc861ddc7d9d50979231554aea2e76b82d74d35f92a6218a6d1b3fc4c5b8')

prepare() {
  mv -v "$pkgname-$pkgver+release.autotools" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
