# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Robson Peixoto

pkgname=unzip
pkgver=6.0
_pkgver=${pkgver/./}
pkgrel=14
pkgdesc='For extracting and viewing files in .zip archives'
url='http://infozip.sourceforge.net/UnZip.html'
arch=('x86_64')
license=('custom')
depends=('bzip2' 'bash')
source=("https://downloads.sourceforge.net/infozip/${pkgname}${_pkgver}.tar.gz"
        'https://src.fedoraproject.org/rpms/unzip/raw/rawhide/f/unzip-6.0-overflow-long-fsize.patch'
        'https://src.fedoraproject.org/rpms/unzip/raw/rawhide/f/unzip-6.0-overflow.patch'
        'https://src.fedoraproject.org/rpms/unzip/raw/rawhide/f/unzip-6.0-cve-2014-8140.patch'
        'https://src.fedoraproject.org/rpms/unzip/raw/rawhide/f/unzip-6.0-cve-2014-8141.patch'
        'https://src.fedoraproject.org/rpms/unzip/raw/rawhide/f/unzip-6.0-cve-2014-8139.patch'
        'https://src.fedoraproject.org/rpms/unzip/raw/rawhide/f/unzip-6.0-heap-overflow-infloop.patch')
sha1sums=('abf7de8a4018a983590ed6f5cbd990d4740f8a22'
          'e8fbdd7388f2ea13a1c2f0e8b278268e02b67bd1'
          'e8c0bc17c63eeed97ad62b86845d75c849bcf4f8'
          'adcf7d53fcc14bf505938d9336c5c3c131c17cb8'
          '27709673b38ffea4efd004d9fb8fa62916bf50d4'
          '7d5283893dc89cb163950de83a88920be7aab1d2'
          '393b6ae0da77c436209eeb0354fa2712500b00b4')

prepare() {
	cd "${srcdir}/${pkgname}${_pkgver}"
	sed -i "/MANDIR =/s#)/#)/share/#" unix/Makefile
	patch -p1 -i ../unzip-6.0-overflow-long-fsize.patch #FS#44171
	patch -p1 -i ../unzip-6.0-overflow.patch #FS#44171
	patch -i ../unzip-6.0-cve-2014-8140.patch # FS#43391
	patch -i ../unzip-6.0-cve-2014-8141.patch # FS#43300
	patch -i ../unzip-6.0-cve-2014-8139.patch # FS#43300
	patch -p1 -i ../unzip-6.0-heap-overflow-infloop.patch # FS#46955
}

build() {
	cd "${srcdir}/${pkgname}${_pkgver}"

	# DEFINES, make, and install args from Debian
	DEFINES='-DACORN_FTYPE_NFS -DWILD_STOP_AT_DIR -DLARGE_FILE_SUPPORT \
		-DUNICODE_SUPPORT -DUNICODE_WCHAR -DUTF8_MAYBE_NATIVE -DNO_LCHMOD \
		-DDATE_FORMAT=DF_YMD -DUSE_BZIP2 -DNOMEMCPY -DNO_WORKING_ISPRINT'

	make -f unix/Makefile prefix=/usr \
		D_USE_BZ2=-DUSE_BZIP2 L_BZ2=-lbz2 \
		LF2="$LDFLAGS" CF="$CFLAGS $CPPFLAGS -I. $DEFINES" \
		unzips
}

package() {
	cd "${srcdir}/${pkgname}${_pkgver}"
	make -f unix/Makefile prefix="${pkgdir}"/usr install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
