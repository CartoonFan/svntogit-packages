# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=wireplumber
pkgname=('wireplumber' 'wireplumber-docs')
pkgver=0.4.5
pkgrel=1
pkgdesc="Session / policy manager implementation for PipeWire"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
makedepends=('cmake' 'cpptoml' 'doxygen' 'glib2' 'gobject-introspection'
'hotdoc' 'lua' 'meson' 'pipewire' 'python-breathe' 'python-sphinx_rtd_theme'
'python-sphinx' 'systemd')
checkdepends=('pipewire-alsa' 'pipewire-jack' 'pipewire-pulse')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/pipewire/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b0d5962e7a83709cc3115fbf0a04b63660085aeea2ebda6d78d692065ccb193cea8b44bb506fa440cf0b88bfae71a4f69a192cd6cba885ebf3c9270bea50c67a')
b2sums=('177984901790228d8ddeb8ee2a548eb53db07c8077734590c3f964df36cfa47a4099e049ae9a19a136f4147c75b7122359a73b8387a641768f71484b9c4ab119')

build() {
  cd "$pkgname-$pkgver"
  arch-meson \
    -Dsystem-lua=true \
    -Delogind=disabled \
    build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test || echo "Known to fail: https://gitlab.freedesktop.org/pipewire/wireplumber/-/issues/18"
}

package_wireplumber() {
  depends=('gcc-libs' 'glibc' 'lua' 'libgio-2.0.so' 'libglib-2.0.so'
  'libgmodule-2.0.so' 'libgobject-2.0.so' 'libpipewire-0.3.so')
  optdepends=('wireplumber-docs: for documentation')
  provides=('libwireplumber-0.4.so' 'pipewire-session-manager')
  conflicts=('pipewire-media-session')
  install="${pkgname}.install"

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {NEWS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove files provided by wireplumber-docs
  rm -frv "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_wireplumber-docs() {
  pkgdesc+=" (documentation)"

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # remove files provided by wireplumber
  rm -frv "${pkgdir}/usr/"{bin,include,lib,share/{gir*,wireplumber}} "${pkgdir}/etc/"
}
