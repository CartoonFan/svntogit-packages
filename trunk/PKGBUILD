# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgbase=devhelp
pkgname=(devhelp devhelp-docs)
pkgver=41.2
pkgrel=2
pkgdesc="API documentation browser for GNOME"
url="https://wiki.gnome.org/Apps/Devhelp"
arch=(x86_64)
license=(GPL)
depends=(webkit2gtk-4.1)
makedepends=(git appstream-glib gobject-introspection meson yelp-tools
             gi-docgen)
_commit=f05e288031ba171e8a0a589d65cba693487726d9  # tags/41.2^0
source=("git+https://gitlab.gnome.org/GNOME/devhelp.git#commit=$_commit"
        0001-gir-Fix-build-with-WebkitGTK-4.1.patch
        0002-docs-Fix-build-with-WebkitGTK-4.1.patch)
sha256sums=('SKIP'
            'b9ee743c67d0e2f290c336a5925e485996c280da66306fb8106a9f4b91c85ba7'
            '0bad708906ccb27c32c1f37c4fb39769929c0482dcf538d6065f12614cc5ce8f')

pkgver() {
  cd devhelp
  git describe --tags | sed 's/\.alpha/alpha/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd devhelp
  git apply -3 ../0001-gir-Fix-build-with-WebkitGTK-4.1.patch
  git apply -3 ../0002-docs-Fix-build-with-WebkitGTK-4.1.patch
}

build() {
  arch-meson devhelp build \
    -D gtk_doc=true \
    -D plugin_emacs=true \
    -D plugin_gedit=true \
    -D plugin_vim=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_devhelp() {
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/doc
}

package_devhelp-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}
