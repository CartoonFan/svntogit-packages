# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-headers
_pkgname=Vulkan-Headers
pkgver=1.2.175
pkgrel=2
epoch=1
pkgdesc="Vulkan header files"
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('APACHE')
makedepends=(cmake git)
provides=("vulkan-hpp=${pkgver}")
groups=(vulkan-devel)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Headers/archive/v${pkgver}.tar.gz"
        0001-Omitted-static-vk_video-headers-in-the-1.2.175-updat.patch
        0002-Add-vk_video-headers-to-CMake-install-target.patch)
sha256sums=('36d8ce49fc617b9a7610ecac1de0054cb059e2c2804febb64c916365d2e39831'
            '47dfe4f9a9fbb58c83dcb91cccdbeb5f36424b710cd8f7012ef32113dc128dac'
            'e7efda7cecf19f143c3be418b76a2cd6d6874009315cdd3a102b36c43882373a')

prepare() {
  cd Vulkan-Headers*

  # fix building error with vulkan-icd-loader-1.2.174
  patch -Np1 -i ../0001-Omitted-static-vk_video-headers-in-the-1.2.175-updat.patch
  patch -Np1 -i ../0002-Add-vk_video-headers-to-CMake-install-target.patch
}

build() {
  cd Vulkan-Headers*

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd Vulkan-Headers*/build
  
  make DESTDIR="${pkgdir}" install
}
