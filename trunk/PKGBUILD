# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libopenmpt
pkgver=0.5.14
pkgrel=1
pkgdesc="A library to render tracker music to a PCM audio stream"
arch=(x86_64)
url="https://lib.openmpt.org/libopenmpt/"
license=(BSD)
depends=(
  flac libFLAC.so
  gcc-libs
  glibc
  libogg libogg.so
  libpulse libpulse.so libpulse-simple.so
  libsndfile libsndfile.so
  libvorbis libvorbis.so libvorbisfile.so
  mpg123 libmpg123.so
  portaudio libportaudio.so
)
makedepends=(autoconf-archive doxygen help2man)
# TODO: remove provides/replaces on openmpt123 after 2022-05-01
provides=(libopenmpt.so openmpt123)
replaces=(openmpt123)
source=(
  "$pkgname-$pkgver.tar.gz::https://lib.openmpt.org/files/libopenmpt/src/${pkgname}-${pkgver}+release.autotools.tar.gz"
)
sha512sums=('8897549c18afb57c5bf54f4dd8a1d80983829bca958052d76cda1315ef4a37f4201575817b7cedfe17a44ee79051a52aa35b26a51b554091e92c9d2dcf5924a3')
b2sums=('ab6ff8db2e19d859fecca882aea3e569b58d96d5055ec0dd42494a7eb38fefa9c8b88f4700d354de71346c71c6d9a96327db1c2b7b48e227928dcba0ce7347d6')

prepare() {
  mv -v "$pkgname-$pkgver+release.autotools" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
