# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libhandy
pkgver=1.2.0
pkgrel=2
pkgdesc="Library full of GTK+ widgets for mobile phones"
url="https://gitlab.gnome.org/GNOME/libhandy"
arch=(x86_64)
license=(LGPL)
depends=(glib2 gtk3)
makedepends=(git gobject-introspection meson vala glade)
checkdepends=(xorg-server-xvfb)
provides=(libhandy-1.so)
_commit=7b38a860ffcec6c2ad28153358cc3d037ddb618f  # tags/1.2.0^0
source=("git+https://gitlab.gnome.org/GNOME/libhandy.git#commit=$_commit"
        0001-meson-Avoid-a-trailing-slash-on-libdir-and-datadir.patch)
sha256sums=('SKIP'
            'ac4f3a4501a93dd47a6b7790204b39d4e77d2037452ff114980b106929b764eb')

prepare() {
  cd libhandy

  # https://gitlab.gnome.org/GNOME/gnome-characters/-/issues/86
  git apply -3 ../0001-meson-Avoid-a-trailing-slash-on-libdir-and-datadir.patch
}

pkgver() {
  cd libhandy
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson libhandy build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
