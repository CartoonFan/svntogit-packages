# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdav
pkgver=5.81.0
pkgrel=1
epoch=1
pkgdesc="A DAV protocol implemention with KJobs"
arch=(x86_64)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kio qt5-xmlpatterns)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0b00fdf902b1aeadfefe9caa11fad52a26f18e4c0e747c1bf8d9a1758b8bb133'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
