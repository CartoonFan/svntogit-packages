# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Miguel Revilla <yo at miguelrevilla.com>
# Contributor: David Sotelo <dvsotelo at gmail.com>
# Contributor: Nuno Araujo <nuno.araujo@russo79.com>

pkgname=qpdf
pkgver=10.6.1
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="https://github.com/qpdf/qpdf"
license=('custom:Artistic-2.0' 'Apache')
depends=('libjpeg-turbo' 'zlib' 'gnutls')
makedepends=('texlive-bin' 'texlive-core' 'python-sphinx' 'python-sphinx_rtd_theme' 'texlive-latexextra')
#optdepends=('perl:	required for fix-qdf')
#checkdepends=('perl')
# https://github.com/qpdf/qpdf/releases/download/release-qpdf-10.6.1/qpdf-10.6.1.sha256
source=(https://github.com/qpdf/qpdf/releases/download/release-${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('4c56328e1eeedea3d87eb79e1fe09374a923fe28756f7a56bcff58523617f05f'
            'SKIP')
validpgpkeys=('C2C96B10011FE009E6D1DF828A75D10998012C7E') # "Jay Berkenbilt <ejb@ql.org>"

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static \
    --enable-crypto-gnutls \
    --disable-crypto-openssl \
    --disable-implicit-crypto \
    --enable-show-failed-test-output \
    --enable-doc-maintenance --enable-html-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check # passes all
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  install -Dm644 manual/build/singlehtml/index.html "${pkgdir}"/usr/share/doc/qpdf/qpdf-manual.html
  install -Dm644 manual/build/latex/qpdf.pdf "${pkgdir}"/usr/share/doc/qpdf/qpdf-manual.pdf

  install -Dm644 completions/bash/qpdf "${pkgdir}/usr/share/bash-completion/completions/qpdf"
  install -Dm644 completions/zsh/_qpdf "${pkgdir}/usr/share/zsh/site-functions/_qpdf"

  rm -rf "${pkgdir}"/usr/share/doc/qpdf/README-doc.txt

  mkdir -m755 -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 {Artistic-2.0,LICENSE.txt} "${pkgdir}"/usr/share/licenses/${pkgname}/
}
