# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=openexr
pkgver=3.0.2
pkgrel=1
pkgdesc='A high dynamic-range image file format library'
url='https://www.openexr.com/'
arch=(x86_64)
license=(BSD)
depends=(zlib imath)
makedepends=(cmake)
source=(https://github.com/openexr/openexr/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/AcademySoftwareFoundation/openexr/commit/f60b55b1.patch)
sha256sums=('afcfaaba700d0bc86a54e9c2e51b22b9dd458527571046f02951ef75ddc7c8e2'
            '0bc7ab7a31a912784584728b3ee9d8b8027a966a9c9e893961467b083f95df91')

prepare() {
  patch -d $pkgname-$pkgver -p1 < f60b55b1.patch # Fix make install
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
