# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Sergej Chodarev <sergejx@centrum.sk>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=40.1
pkgrel=3
pkgdesc="Collection of plugins for the gedit Text Editor"
url="https://wiki.gnome.org/Apps/Gedit/ShippedPlugins"
arch=(x86_64)
license=(GPL)
depends=(gedit python-dbus python-cairo)
makedepends=(vte3 libgit2-glib gucharmap gobject-introspection vala yelp-tools
             appstream-glib git meson)
optdepends=('gucharmap: for charmap plugin'
            'vte3: for embedded terminal'
            'libgit2-glib: for git plugin')
_commit=ff3e9f6b51698b91df2ccede481809860111e91b  # tags/40.1^0
source=("git+https://gitlab.gnome.org/GNOME/gedit-plugins.git#commit=$_commit"
        fix-build.diff)
sha512sums=('SKIP'
            '3ee6d0918d9b27dc8f43d3433461606ad066a6a0f662d0f8d39ccbfc6f77c3abef3245e266395eb8b4e8a2518148ecf13c96bdc88aff594776c487555e9cb632')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../fix-build.diff  # meson 0.61
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr "$pkgdir/usr"
  python -O -m compileall -d /usr "$pkgdir/usr"
}

# vim:set sw=2 et:
