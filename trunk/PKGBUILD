# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=atkmm-2.36
pkgname=(atkmm-2.36 atkmm-2.36-docs)
pkgver=2.36.1
pkgrel=1
pkgdesc="C++ bindings for ATK"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(atk glibmm-2.68)
makedepends=(git mm-common glibmm-2.68-docs meson)
options=(!emptydirs)
_commit=31bc39c17bda0b12e3f6d432ef9e618897d5653b  # tags/2.36.1^0
source=("git+https://gitlab.gnome.org/GNOME/atkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd atkmm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd atkmm
}

build() {
  arch-meson atkmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_atkmm-2.36() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_atkmm-2.36-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
