# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-3d
pkgname=(python-pyqt6-3d)
pkgver=6.0.2
pkgrel=1
pkgdesc="Python bindings for Qt3D"
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqt3d/intro"
license=(GPL3)
groups=(pyqt6)
depends=(qt6-3d python-pyqt6)
makedepends=(sip pyqt-builder)
source=("https://pypi.python.org/packages/source/P/PyQt6-3D/PyQt6_3D-$pkgver.tar.gz")
sha256sums=('fb797dda5571e169edf60a0fd1443fac0907fb1f147339dbfdcd4a671106a876')

build() {
  cd PyQt6_3D-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake-qt6
  cd build
  make
}

package_python-pyqt6-3d(){
  cd PyQt6_3D-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
