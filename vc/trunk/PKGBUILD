# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgver=1.4.1
pkgrel=3
pkgdesc='A library to ease explicit vectorization of C++ code'
url='https://github.com/VcDevel/Vc'
arch=(x86_64)
license=(LGPL3)
makedepends=(cmake)
source=(https://github.com/VcDevel/Vc/releases/download/$pkgver/Vc-$pkgver.tar.gz
        https://github.com/VcDevel/Vc/commit/f41958c3.patch)
sha256sums=('68e609a735326dc3625e98bd85258e1329fb2a26ce17f32c432723b750a4119f'
            'cd916fbf9de5650ef4e9889c0022226623d411b0dd8d45c718bc8051c5ff637c')

prepare() {
  patch -d Vc-$pkgver -p1 < f41958c3.patch # Add missing include for GCC 11
}

build() {
  cmake -B build -S Vc-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DTARGET_ARCHITECTURE=generic
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
