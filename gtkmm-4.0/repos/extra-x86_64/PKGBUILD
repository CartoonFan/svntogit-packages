# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm-4.0
pkgname=(gtkmm-4.0 gtkmm-4.0-docs)
pkgver=4.2.0
pkgrel=1
pkgdesc="C++ bindings for GTK 4"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(gtk4 pangomm-2.48 atkmm-2.36)
makedepends=(git mm-common glibmm-2.68-docs cairomm-1.16-docs pangomm-2.48-docs
             atkmm-2.36-docs meson)
checkdepends=(xorg-server-xvfb)
options=(!emptydirs)
_commit=e0e280baf48b9899b1870a3308ea8fb31c83ebb5  # tags/4.2.0^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtkmm
}

build() {
  arch-meson gtkmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_gtkmm-4.0() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_gtkmm-4.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
