# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gdk-pixbuf2
pkgver=2.42.4
pkgrel=1
pkgdesc="An image loading library"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/GdkPixbuf"
license=(LGPL)
depends=(glib2 libpng libtiff libjpeg shared-mime-info)
makedepends=(gobject-introspection git meson gi-docgen docbook-xsl)
provides=(libgdk_pixbuf-2.0.so)
install=gdk-pixbuf2.install
_commit=88260c7794f60b5fee2127305d0b4bcfae0e8697  # tags/2.42.4^0
source=("git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#commit=$_commit"
        gdk-pixbuf-query-loaders.hook)
sha256sums=('SKIP'
            '9fb71d95e6a212779eb0f88dde5a3cee0df7f4d9183f8f9ce286f8cdc14428f0')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gdk-pixbuf
}

build() {
  arch-meson gdk-pixbuf build \
    -D builtin_loaders=all \
    -D gtk_doc=true \
    -D installed_tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set ts=2 sw=2 et:
