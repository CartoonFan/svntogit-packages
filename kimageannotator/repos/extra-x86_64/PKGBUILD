# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kimageannotator
pkgver=0.5.0
pkgrel=2
pkgdesc='Tool for annotating images'
arch=(x86_64)
url='https://github.com/DamirPorobic/kImageAnnotator'
license=(GPL)
depends=(qt5-svg kcolorpicker)
makedepends=(cmake qt5-tools)
source=(https://github.com/ksnip/kImageAnnotator/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/ksnip/kImageAnnotator/commit/eda12cf4.patch)
sha256sums=('9507382c76997dcce7d080d26081737ae3ed570221039e32012b6196f522b36f'
            '7809128106bf2b0d1bb18af983af7b54ec627b3b5ae575f9dedd5937481bca13')

prepare() {
  patch -d kImageAnnotator-$pkgver -p1 < eda12cf4.patch # Fix crash on destructor
}

build() {
  cmake -B build -S kImageAnnotator-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

