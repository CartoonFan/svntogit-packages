# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gi-docgen
pkgver=2021.2+90+g7fbbaf3
pkgrel=1
pkgdesc="Documentation generator for GObject-based libraries"
url="https://gitlab.gnome.org/ebassi/gi-docgen"
arch=(any)
license=(Apache GPL3)
depends=(python-jinja python-markdown python-markupsafe python-pygments
         python-toml python-typogrify)
makedepends=(python-setuptools python-pip git)
_commit=7fbbaf31aaa1f993bc42a79ff22fffdc168cc664  # main
source=("git+$url.git#commit=$_commit" fix.diff)
sha256sums=('SKIP'
            'aa0a255c883fb19276f35f107c967cd961cb22e7e67a6b6f10048fef19301675')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../fix.diff
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
