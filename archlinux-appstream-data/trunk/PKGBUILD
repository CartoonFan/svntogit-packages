# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20210902
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('2ae207b054bb35f1f95afed7fede71d3c794147b7634ef7961ef06db59c7c33c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '4895cf560a2f5bd7d59214b22bf4b3ce6ecf87cedb52ba354421de0de9a5b19c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '3b418445248a31146cf02f5377d6f0a92d453fba4ed3901e8ce62ae04a7abbf6'
            'fdda88c10bd1549579431c1234183caa99f33f5a4b63556345559d2d13ad222d'
            '0a490f907894f4b4142e98100acc56d9214c71ba88af34231b85d803ec9ebc59'
            '48fffc74c5ac26e542248fd3b0e7b8c3df676cccc562e07cd2871462bb20c9f2'
            'bf789c1dc2754982f7a3804caf93c331dc979dfe723aa284e0e4bff99b9bee67'
            '5db1984e409c7a71e0ac490dd660cbe199d59ee02f8281d010ce7ec491fcbc81'
            '33141ad437a677381e714df7058b21e6070132545c90922faf0a9d5fa5780902'
            '4e6d51e664538f55cc33d2d91abb00a5949b2c03dcc942780bfb79faacd20122')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
