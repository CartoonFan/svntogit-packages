# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=pipewire-media-session
pkgname=(pipewire-media-session pipewire-media-session-docs)
pkgver=0.4.0
pkgrel=1
epoch=1
pkgdesc="Low-latency audio/video router and processor - Session manager"
url="https://pipewire.org"
license=(MIT)
arch=(x86_64)
makedepends=(git meson doxygen graphviz 'pipewire>=0.3.39' systemd dbus
             alsa-lib)
_commit=5fd4b081fecbdb250e2f7f6deb8e6ffaf68f6dca  # tags/0.4.0
source=("$pkgbase::git+https://gitlab.freedesktop.org/pipewire/media-session.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D docs=enabled
    -D with-module-sets=[]
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_pipewire-media-session() {
  depends=('pipewire>=0.3.39' libpipewire-0.3.so libsystemd.so libdbus-1.so
           libasound.so)
  optdepends=('pipewire-media-session-docs: Documentation')
  provides=(pipewire-session-manager)
  install=pipewire-media-session.install

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"

    _pick docs usr/share/doc
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgbase" -m644 $pkgbase/COPYING
}

package_pipewire-media-session-docs() {
  pkgdesc+=" - documentation"

  mv docs/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/COPYING
}

# vim:set sw=2 et:
