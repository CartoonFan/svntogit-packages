# Contributor: Sergej Pupykin
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=vde2
pkgver=2.3.2
pkgrel=19
pkgdesc="Virtual Distributed Ethernet for emulators like qemu"
url="https://sourceforge.net/projects/vde/"
license=("GPL" "LGPL" "CUSTOM")
arch=('x86_64')
depends=('bash' 'libpcap' 'openssl')
makedepends=('python')
source=(https://downloads.sourceforge.net/vde/$pkgname-$pkgver.tar.bz2
        vde_cryptcab-compile-against-openssl-1.1.0.patch
        dhcpd.conf.sample
        iptables.rules.sample
        vde-config.sample
        vde-connection.sample
        https://github.com/virtualsquare/vde-2/commit/7f7593cd.patch
        https://github.com/virtualsquare/vde-2/commit/738d5149.patch
        https://github.com/virtualsquare/vde-2/commit/561701bb.patch)
install=vde2.install
options=(!makeflags)
sha256sums=('cbea9b7e03097f87a6b5e98b07890d2275848f1fe4b9fcda77b8994148bc9542'
            '110370a5f48f1e241d43f8bb5e3ea6d2ca7d2c1949e1cf672d03bfc897f2e11f'
            'da0e2766dc63069da929c28126831ad5fdddcc4a04105a21217d78832c7ca1bc'
            '99076d7466cd99673dbe91ef83865187e7868177959b38e125df63eea957f83e'
            '5727c215646333c37b26388146cd3e6b3814b88d60d54051d7da99e00c0aef87'
            '5139110ed6d5d1174bf12971512dac5196d9d07df46dd393d7b1cd083118fe9b'
            'a0d020f687254b12776ec18f60ce734fbc92ed4d6f5d81a1fba040d73762c2e1'
            'efad3a713b85265cd9691d8df513a1f7ebd678fc133f8efc2778b806cebf5ba1'
            '66806a7f893947ab009bd1dae71d2bbb713ae837e0e12c08047c007269d4ce29')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../vde_cryptcab-compile-against-openssl-1.1.0.patch
# Fix format-security build errors
  patch -p2 -i ../7f7593cd.patch
  patch -p2 -i ../738d5149.patch
  patch -p1 -i ../561701bb.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --libexecdir=/usr/lib/vde2 \
             --enable-experimental
  make
}

package() {
  cd $pkgname-$pkgver 
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" sbindir="$pkgdir/usr/bin" libexecdir="$pkgdir/usr/lib/vde2" install
  install -D -m 644 ../vde-config.sample "$pkgdir"/etc/vde/vde-config.sample
  install -D -m 644 ../vde-connection.sample "$pkgdir"/etc/vde/vde-connection.sample
  install -D -m 644 ../dhcpd.conf.sample "$pkgdir"/usr/share/vde2/dhcpd.conf.sample
  install -D -m 644 ../iptables.rules.sample "$pkgdir"/usr/share/vde2/iptables.rules.sample
  # install slirp license
  install -D -m 644 COPYING.slirpvde "$pkgdir"/usr/share/licenses/vde2/COPYING.slirpvde
}
