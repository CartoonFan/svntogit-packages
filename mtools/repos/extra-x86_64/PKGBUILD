# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.32
pkgrel=1
epoch=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
url="https://www.gnu.org/software/mtools/"
license=('GPL3')
depends=('bash' 'glibc')
makedepends=('libx11' 'texinfo')
optdepends=('libx11: required by floppyd'
            'libxau: required by floppyd')
backup=("etc/${pkgname}.conf")
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('a66e7483c422abdaddbe9e6b2cd0ecc2f87b0bc4f40929ac2e4db8b8d88cfacf118b126cba3207307bff376d5572170a1ee5803618de0544f68d420ed06bcab9'
            'SKIP')
b2sums=('6b057be3d053ecf1ebee72b6b73324ec2cdbd7daf87c927f6ec2f6ef203345ddb869d6d7cb645e21054304ae68fc7a33e77e7a0f0cb606d3b755da36e66ca69a'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  cd "${pkgname}-${pkgver}"
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i mtools.conf
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 mtools.conf -t "${pkgdir}/etc/"
  install -vDm 644 {README,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
