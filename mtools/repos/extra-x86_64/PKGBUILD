# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.29
pkgrel=1
epoch=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
url="https://www.gnu.org/software/mtools/"
license=('GPL3')
depends=('bash' 'glibc')
makedepends=('libx11' 'texinfo')
optdepends=('libx11: required by floppyd'
            'libxau: required by floppyd')
backup=("etc/${pkgname}.conf")
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('3671fa835bf7b410b33acbe0e4694da1609f284a99268e7c64ebdbf3ce9fc9e78cc5594d06eae5487e380b983ead190098d01a9fc1bc88b4eda324046172e62e'
            'SKIP')
b2sums=('b448fad8f5669230dc4fe32442c3a8b0ee570e1b084ac6b99b3239fa16fa97ba0ac5263aee34fefcb8726999178829a9ad6ed0444f41361518e5d24cfb3de083'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  cd "${pkgname}-${pkgver}"
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i mtools.conf
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 mtools.conf -t "${pkgdir}/etc/"
  install -vDm 644 {README,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
