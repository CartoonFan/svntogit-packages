# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Roja <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=rocs
pkgver=21.03.90
pkgrel=1
pkgdesc="Graph Theory IDE"
url="https://kde.org/applications/education/rocs/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(grantlee ktexteditor kdeclarative qt5-xmlpatterns qt5-script hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('86c8dd0def4b7b32cdf7dbcfb85fbc028404448ca997cb6d67aabbbc4b20f49e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
