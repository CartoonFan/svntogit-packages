# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgname=archinstall
pkgver=2.1.4
pkgrel=3
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=('any')
url="https://github.com/archlinux/archinstall"
license=('GPL3')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
provides=('python-archinstall')
conflicts=('python-archinstall')
replaces=('python-archinstall')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz.asc")
md5sums=('b5846e2a8aacfbba2ccc00643a9bb1f7'
         'SKIP')
sha512sums=('8d5c573aace6ffe1716121ae2417170db5330cd9b2f458a6454ccb12d1c946dd25452f349bd5e9a8f3f1d21ed55ffe1b82311db44bdf20a6579a89248ffa383c'
            'SKIP')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>


build() {
	cd "$pkgname-$pkgver"

        python setup.py build

        cd docs
        make man
}


package() {
	cd "$pkgname-$pkgver"

        python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

        install -Dm644 docs/_build/man/archinstall.1 "${pkgdir}"/usr/share/man/man1/archinstall.1
}
