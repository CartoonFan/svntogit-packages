# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: jepaan <jepaan at hotmail dot com>

pkgname=assimp
pkgver=5.1.1
pkgrel=1
pkgdesc="Library to import various well-known 3D model formats in an uniform manner"
arch=('x86_64')
license=('BSD')
depends=('zlib' 'gcc-libs')
makedepends=('cmake' 'ninja')
url='https://assimp.org/'
source=("$pkgname-$pkgver.tar.gz::https://github.com/assimp/assimp/archive/v${pkgver}.tar.gz")
sha512sums=('535dec60c09213e388edc553e18b97679f8ed649546ae13bdbfe66cf02916b79ad70eab42304b64b53bfbf40a26d998a27d93c65afda0d5fa39b6fb42dad8a2f')

build() {
    cd ${pkgname}-${pkgver}

    cmake . \
        -Bbuild \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DASSIMP_BUILD_SAMPLES=OFF
    ninja -C build
}

package()  {
    cd ${pkgname}-${pkgver}

    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    # Remove this in release > 5.0.1
    # https://bugs.archlinux.org/task/67735
    sed -i "s|includedir=.*|includedir=/usr/include/assimp|" "${pkgdir}"/usr/lib/pkgconfig/assimp.pc
}
