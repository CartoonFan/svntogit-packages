# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=3.36.1
pkgrel=2
pkgdesc="Location and timezone database and weather-lookup library"
url="https://wiki.gnome.org/Projects/LibGWeather"
arch=(x86_64)
license=(LGPL)
depends=(libsoup gtk3 geocode-glib dconf)
makedepends=(gobject-introspection vala git gtk-doc glade meson)
provides=(libgweather-3.so)
_commit=687c63f68b0a3561731889c14fb28ac5c52d5be0  # tags/3.36.1-fixed^0
source=("git+https://gitlab.gnome.org/GNOME/libgweather.git#commit=$_commit"
        0001-metno-Use-compat-2.0-API.patch
        0002-metno-Use-GNOME-specific-subdomain.patch
        0003-metno-Use-as-arguments-separator-in-query.patch
        0004-metno-Use-alphabetical-2.0-symbol-codes.patch
        0005-gweather-Only-use-4-significant-decimals-for-locatio.patch)
sha256sums=('SKIP'
            '52adc44450035384038c53583998592224c17f51e6872b12cb186f30d52cb5da'
            'bdb071f6af22a551eee6d2fc81e7cb19ab56048b6e898f4bc5b0b7cecd185722'
            'aa5050f1a36a17444c893346ab2343226e60bf273be95f94401f7124863d0e61'
            '7a6fafa664f64c5ec1085abaacbc2ac384f0833df05550f3c8bbac671b1adb54'
            'ba2d2af73ae848350f2eb484cea12ee01b623e78273e5a4854ccee0ac81e4cde')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-fixed//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Backport some changes to fix yr.no; patch selection taken from debian
  # https://bugs.archlinux.org/task/69919
  git apply -3 ../*.patch
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  # Test fails without fr_FR.UTF-8 locale
  meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
