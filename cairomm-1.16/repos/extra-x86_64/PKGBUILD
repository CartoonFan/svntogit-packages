# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Kevin <kevin@archlinux.org>
# Contributor: criminy <criminy@gmail.com>

pkgbase=cairomm-1.16
pkgname=(cairomm-1.16 cairomm-1.16-docs)
pkgver=1.16.1
pkgrel=1
pkgdesc="C++ bindings for Cairo"
url="https://www.cairographics.org/cairomm/"
arch=(x86_64)
license=(LGPL MPL)
depends=(cairo libsigc++-3.0)
makedepends=(git mm-common meson)
options=(!emptydirs)
_commit=61286896d11ed961add217ff4a209d10d9efb700  # tags/1.16.1^0
source=("git+https://gitlab.freedesktop.org/cairo/cairomm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd cairomm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd cairomm
}

build() {
  arch-meson cairomm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_cairomm-1.16() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_cairomm-1.16-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
