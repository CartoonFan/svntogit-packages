# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=aom
pkgver=3.0.0
pkgrel=1
pkgdesc="Alliance for Open Media video codec"
url="https://aomedia.org/"
arch=(x86_64)
license=(BSD custom:PATENTS)
depends=(gcc-libs)
makedepends=(git cmake ninja doxygen yasm)
provides=(libaom.so)
source=("$pkgname-$pkgver.tar.gz::https://aomedia.googlesource.com/aom/+archive/v$pkgver.tar.gz")
b2sums=('572af4b99669f2aff52748446fe985373fdfabe9dedfcc484c8346937a6adb80c60e251126ca529c75f6ace24c6c9a9d71bbf7eede49f07a97fde57ee04494ad')

build() {
  cmake -S . -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=1 \
    -DENABLE_TESTS=0
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  mkdir -p "$pkgdir/usr/share/doc"
  cp -r build/docs/html "$pkgdir/usr/share/doc/$pkgname"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE PATENTS
}

# vim:set et sw=2:
