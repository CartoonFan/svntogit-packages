# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=21.03.80
pkgrel=1
pkgdesc="KDE File Manager & Web Browser"
arch=(x86_64)
url="https://konqueror.org/"
license=(LGPL)
depends=(dolphin keditbookmarks qt5-webengine qt5-script)
makedepends=(extra-cmake-modules kdoctools kdesu)
optdepends=('kdesu: shell command plugin')
groups=(kde-applications kde-network)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2dfe1b44d4ee9d1d3331b4c653c58e6c695d2ecf55c5bf0b3cd6a00e0a09ce2a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
