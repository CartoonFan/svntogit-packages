# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Contributor: Elvis Angelaccio <elvis.angelaccio@kdemail.net>
# Contributor: Gustavo alvarez <sl1pkn07@gmail.com>

pkgname=kwave
pkgver=21.11.90
pkgrel=1
pkgdesc='A sound editor'
arch=(x86_64)
url='https://apps.kde.org/kwave/'
license=(GPL)
depends=(kio audiofile libmad libsamplerate hicolor-icon-theme id3lib fftw)
makedepends=(extra-cmake-modules kdoctools doxygen librsvg opus)
optdepends=('opus: OGG plugin')
groups=(kde-applications kde-multimedia)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ec65e864639a0d6e38b6390c5f0f8797e22a807c515c04db7faf4d01a01b0dba'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
