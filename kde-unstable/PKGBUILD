# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=21.07.90
pkgrel=2
pkgdesc='Data model and extraction system for travel reservation information'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmime kpkpass kcalendarcore kcontacts poppler libphonenumber zxing-cpp)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        poppler-21.8.0.patch)
sha256sums=('a70e41377b7f4984f171c084276d9358a3018fbe42ff150c95e7169f53bbfacd'
            'SKIP'
            'ac4cae76f3d4392c4dc3c41f0a0ee26d79c9937f4451736f64cd114bdaa94d28')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -Np1 -d $pkgname-$pkgver <poppler-21.8.0.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
