# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kuserfeedback
pkgver=1.0.0
pkgrel=3
pkgdesc='Framework for collecting user feedback for applications via telemetry and surveys'
arch=(x86_64)
url='https://kde.org/products/frameworks/'
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools clang qt5-charts qt5-svg qt5-declarative)
optdepends=('qt5-declarative: QML bindings' 'qt5-charts: User Feedback console' 'qt5-svg: User Feedback console')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/libraries/kuserfeedback/-/commit/17db7d75.patch)
sha256sums=('5a2f53ebb4b99a280757ca32bd9b686a7764a726e7e4d8bafee33acbb44b9db7'
            'SKIP'
            '06ca3a53b3256e7314344fd96b3483b15a7c932147df57798126ddc77003887e')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 17db7d75.patch # Fix crashes
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
