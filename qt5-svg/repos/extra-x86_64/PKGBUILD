# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-svg
_qtver=5.15.2
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for displaying the contents of SVG files'
depends=('qt5-base')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         CVE-2021-3481a.patch::https://code.qt.io/cgit/qt/qtsvg.git/patch/?id=aceea78c
         CVE-2021-3481b.patch::https://code.qt.io/cgit/qt/qtsvg.git/patch/?id=0fa52290)
sha256sums=('8bc3c2c1bc2671e9c67d4205589a8309b57903721ad14c60ea21a5d06acb585e'
            '9e2e001d3ee7376ef1729e5fd77b662cb8632a14462b56e5b9181c7bc4e2bce4'
            '60c37f206f855f77ac7fa5a3d42166bc349db4b31a088471d9697d297eea4763')

prepare() {
  mkdir -p build
  patch -d $_pkgfqn -p1 < CVE-2021-3481a.patch
  patch -d $_pkgfqn -p1 < CVE-2021-3481b.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
