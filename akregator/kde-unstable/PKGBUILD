# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akregator
pkgver=21.03.90
pkgrel=2
pkgdesc="A Feed Reader by KDE"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kontactinterface messagelib knotifyconfig ktexteditor syndication kuserfeedback)
makedepends=(extra-cmake-modules boost kdoctools)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         akregator-qsortfilterproxymodel.patch::https://invent.kde.org/pim/akregator/-/commit/d51a165f.patch)
sha256sums=('10306124027a8bbf0ac4aa62295b91f4d97522fac7e1c842e3db217d07e48e3c'
            'SKIP'
            'f497abdbcf2bfaff51703657b75b4be36c0448d18d1ce145ae47dc7a676e0180')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < akregator-qsortfilterproxymodel.patch # Fix empty article list with Qt post 5.15.2
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
