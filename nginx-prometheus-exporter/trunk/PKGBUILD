# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=nginx-prometheus-exporter
pkgdesc='NGINX Prometheus Exporter for NGINX and NGINX Plus'
pkgver=0.8.0
pkgrel=1
arch=(x86_64)
license=('Apache')
url="https://github.com/nginxinc/nginx-prometheus-exporter"
depends=(glibc)
makedepends=(go)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nginxinc/nginx-prometheus-exporter/archive/v${pkgver}.tar.gz)
sha512sums=('ee49cfc06606494812a9f5218fe16d53dc3edfa6fc67a83f177bb64fceada08215e84dd6cc4b959d741b84a9c09dc029171a204fdfc36a440113f398d933b849')

build() {
  cd "$pkgname-$pkgver"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"  -X main.version=${pkgver} -X main.gitCommit=${pkgver}" \
    .
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
}
