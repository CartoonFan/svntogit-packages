# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-builder
pkgver=3.40.2+8+g5e82493fa
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview4 devhelp libgit2-glib gjs python-gobject sysprof
         desktop-file-utils ctags libpeas vte3 vala python-jedi autoconf-archive
         flatpak-builder gspell libdazzle template-glib jsonrpc-glib
         python-sphinx webkit2gtk glade python-lxml meson libportal)
makedepends=(llvm gobject-introspection gtk-doc yelp-tools appstream-glib git
             mm-common python-sphinx_rtd_theme clang)
checkdepends=(xorg-server-xvfb)
optdepends=('gnome-code-assistance: Legacy assistance services'
            'clang: C/C++ code assistance')
groups=(gnome-extra)
_commit=5e82493fa3c2e42c73a79ce2930606b3860b6dd2  # gnome-builder-3-40
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit"
        screenshots.diff)
sha256sums=('SKIP'
            '4d59233b7a45412ddee49764a6ca901f87eecdd52f1b4a84fbfb630d0a2d5e85')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix screenshots
  git apply -3 ../screenshots.diff
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D help=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libgladeui-2.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
