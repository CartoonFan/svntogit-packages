# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-webengine
_qtver=6.3.0-beta2
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=(qt6-webchannel qt6-positioning libxcomposite libxrandr libxkbfile 
         libevent snappy nss libxslt minizip ffmpeg re2 libvpx libxtst ttf-font pciutils)
makedepends=(cmake ninja python-html5lib gperf jsoncpp qt6-tools pipewire nodejs qt6-websockets)
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/development_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        chromium-93-ffmpeg-4.4.patch
        chromium-94-ffmpeg-roll.patch
        qt6-webengine-system-libxslt.patch::https://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=bcce1113)
sha256sums=('d98e275b82eeb8639785079b033bca17ac543ddb6943545e67e0eccaa5f87ed1'
            '1a9e074f417f8ffd78bcd6874d8e2e74a239905bf662f76a7755fa40dc476b57'
            '56acb6e743d2ab1ed9f3eb01700ade02521769978d03ac43226dec94659b3ace'
            '1a4c12e6036a8bf97d2779e89321a44a4a1a750f1691fd8fcbf3269dea0d7558')

prepare() {
# Patches to build with ffmpeg 4.4; remove when ffmpeg 5.0 moves to stable
  if ! pkg-config --atleast-version 59 libavformat; then
    patch -d $_pkgfn/src/3rdparty/chromium -p1 -i "$srcdir"/chromium-93-ffmpeg-4.4.patch
    patch -d $_pkgfn/src/3rdparty/chromium -Rp1 -i "$srcdir"/chromium-94-ffmpeg-roll.patch
  fi

  patch -d $_pkgfn -p1 < qt6-webengine-system-libxslt.patch # Fix using system libxslt
}
  
build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_TOOLCHAIN_FILE=/usr/lib/cmake/Qt6/qt.toolchain.cmake \
    -DQT_FEATURE_webengine_system_ffmpeg=ON \
    -DQT_FEATURE_webengine_system_icu=OFF \
    -DQT_FEATURE_webengine_system_libevent=ON \
    -DQT_FEATURE_webengine_system_libxslt=ON \
    -DQT_FEATURE_webengine_proprietary_codecs=ON \
    -DQT_FEATURE_webengine_kerberos=ON \
    -DQT_FEATURE_webengine_webrtc_pipewire=ON \
    -DQT_FEATURE_webengine_full_debug_info=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/${_pkgfn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
