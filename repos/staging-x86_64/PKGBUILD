# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=21.04.0
pkgrel=2
pkgdesc="Additional components to increase the functionality of KIO"
arch=(x86_64)
url='https://www.kde.org/'
license=(LGPL)
depends=(kio kdnssd libssh smbclient libmtp phonon-qt5 syntax-highlighting kdsoap-ws-discovery-client libxcursor)
makedepends=(extra-cmake-modules kdoctools gperf taglib libappimage openexr kactivities-stats)
optdepends=('qt5-imageformats: thumbnails for additional image formats' 'perl: info kioslave'
            'kimageformats: thumbnails for additional image formats' 'taglib: audio file thumbnails'
            'libappimage: AppImage thumbnails' 'icoutils: Windows executable thumbnails'
            'openexr: EXR format thumbnails' 'kactivities-stats: recently used kioslave')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kio-extras-openexr3.patch)
sha256sums=('4f4a6f127a29d68e7c8185ff9f67a2c1ceb2df8edaa87bab4bcc974b5869b3dc'
            'SKIP'
            '7d1c0e37f23a86d74e9f84077494cd751736041cdae7669a8e98d32bffbb9a64')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < kio-extras-openexr3.patch # Fix build with openexr 3
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
