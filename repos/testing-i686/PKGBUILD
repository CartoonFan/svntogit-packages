# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=skanlite
pkgver=0.8
pkgrel=2
pkgdesc="Image Scanning Application for KDE"
arch=('i686' 'x86_64')
url='http://kde-apps.org/content/show.php?content=109803'
license=('GPL')
depends=('kdebase-runtime' 'libksane')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://downloads.sourceforge.net/sanewidget/${pkgname}-${pkgver}.tar.bz2")
md5sums=('baa43dbc78be696e121969b0a3d88929')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
