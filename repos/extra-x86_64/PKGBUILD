# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pango
pkgname=(pango pango-docs)
pkgver=1.50.0
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(libthai cairo libxft harfbuzz fribidi)
makedepends=(gobject-introspection help2man git meson gi-docgen)
checkdepends=(ttf-dejavu cantarell-fonts)
_commit=e80e224baabc7bf22971d0a97c8b31d32fa8ca1c  # tags/1.50.0^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        fix-test.diff)
sha256sums=('SKIP'
            'e1c0638305c0f46653bd60b5dee0b3445dd6a6b9aff8cee70be7759c21ba1037')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango

  # Fix tests... seems to be a rounding error
  git apply -3 ../fix-test.diff
}

build() {
  arch-meson pango build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
