# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.7.1
pkgrel=1
pkgdesc='Lightweight image viewer'
arch=('x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
# https://github.com/BestImageViewer/geeqie#optional-libraries
depends=('exiv2' 'gtk3' 'ffmpegthumbnailer'
         'djvulibre' 'libheif' 'libchamplain'
         'poppler-glib' 'libarchive' 'libraw' 'lua')
makedepends=('git'
             'intltool' 'python' 'librsvg'
             'libwmf' 'libwebp' 'imagemagick'
             'fbida' 'gawk' 'perl-image-exiftool'
             # for the docs
             'doxygen' 'yelp-tools' 'graphviz'
)
optdepends=('librsvg: SVG rendering'
            'fbida: for jpeg rotation'
            'gawk: to use the geo-decode function'
            'perl-image-exiftool: for the jpeg extraction plugin'
            'gphoto2: command-line tools for various (plugin) operations'
            'imagemagick: command-line tools for various (plugin) operations')
source=(#"http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz"{,.asc}
        ${pkgname}-${pkgver}.tar.gz::https://github.com/BestImageViewer/geeqie/archive/refs/tags/v${pkgver}.tar.gz
        buildfix.diff
)
sha256sums=('6625f174abfda86883fd94f4cbd1054528db0f90915ae5d00f4a080dcb80411d'
            '1e293e872a8ddce4e5f1e22ce50d1fe29212ca05cac8cc45df5ecdfd308effed')
#validpgpkeys=('85D4CA42952C949B175362B379D0B06F4E20AF1C') # "Klaus Ethgen <Klaus@Ethgen.de>"

# main repo: http://www.geeqie.org/cgi-bin/gitweb.cgi?p=geeqie.git
# bug tracker: https://github.com/BestImageViewer/geeqie/issues

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix missinging Changelog.html
  patch -Np1 -i ../buildfix.diff
  # fix broken docdir
  sed -i "s:+git:${pkgver}:" configure.ac
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/share/doc/geeqie-1.7.1/ChangeLog.gqview "${pkgdir}"/usr/share/doc/geeqie-1.7.1/ChangeLog
}
