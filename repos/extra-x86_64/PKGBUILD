# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=21.04.0
pkgrel=3
pkgdesc='KDE PIM messaging library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmailtransport libgravatar kidentitymanagement kmbox qca)
makedepends=(extra-cmake-modules boost)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        CVE-2021-31855.patch::https://invent.kde.org/pim/messagelib/commit/3b5b171e.patch
        kdebug-282792.patch::https://invent.kde.org/pim/messagelib/commit/53daeb84.patch)
sha256sums=('2fa33e75aef77ce89f91a777b14dafe9f51fffc431dfd975767e830167c65be3'
            'SKIP'
            'a44b119ea2bcc80a131dc8c62cc54f7358c0811e48251c6681b3f8cb877a55b1'
            'd4739f277fde5ec33625e47087d3cea6294331ae342b078368f2d7eac9861acc')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < CVE-2021-31855.patch
  patch -d $pkgname-$pkgver -p1 < kdebug-282792.patch # Fix deletion of multiple attachments
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
