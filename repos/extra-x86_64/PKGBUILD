# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=erofs-utils
pkgver=1.2.1
pkgrel=2
pkgdesc="Userspace utilities for linux-erofs file system"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=('GPL2')
depends=('glibc' 'lz4')
makedepends=('util-linux')
source=("https://git.kernel.org/pub/scm/linux/kernel/git/xiang/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1b0fae6f1d7a5ee01f65e71e389c474d596be8800087beb063bfbda471c54d76f9d3f08da645cecb613b637044ff3f6b19d3c8647425261cec1646c87ad57802')
b2sums=('787d3c549020bf44d750ef1af8878c452683579ff545ed0a6b03a24fb5e5f559dad8dd1d00664598b7d42ec6f9d69800b087d0dcb0de147b51696b416903d712')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
