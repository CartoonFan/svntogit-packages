# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=wireplumber
pkgname=('wireplumber' 'wireplumber-docs')
pkgver=0.4.4
pkgrel=3
pkgdesc="Session / policy manager implementation for PipeWire"
arch=('x86_64')
url="https://gitlab.freedesktop.org/pipewire/wireplumber"
license=('MIT')
makedepends=('cmake' 'cpptoml' 'doxygen' 'glib2' 'gobject-introspection'
'hotdoc' 'lua' 'meson' 'pipewire' 'python-breathe' 'python-sphinx_rtd_theme'
'python-sphinx' 'systemd')
checkdepends=('pipewire-alsa' 'pipewire-jack' 'pipewire-pulse')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.freedesktop.org/pipewire/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f8d764c1728c2e550fc41044a8ebbbeba27157540077648431796d7de5a04281df00d9742b04c6f76b50e446b2a723f2555520cb953d432f90b05148967de4ef')
b2sums=('cc2768db3a4f6f26a176c9fff759384fedb020c98540ecbe1e02e28a7f16fa3ecc4ec25de50a65021cabb4e5746c41c683b714e0bc5cf5003f2a7d9c9414516c')

build() {
  cd "$pkgname-$pkgver"
  arch-meson \
    -Dsystem-lua=true \
    -Dsystemd-system-service=true \
    -Dsystemd-user-service=true \
    -Delogind=disabled \
    build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test || echo "Known to fail: https://gitlab.freedesktop.org/pipewire/wireplumber/-/issues/18"
}

package_wireplumber() {
  depends=('gcc-libs' 'glibc' 'lua' 'libgio-2.0.so' 'libglib-2.0.so'
  'libgmodule-2.0.so' 'libgobject-2.0.so' 'libpipewire-0.3.so')
  optdepends=('wireplumber-docs: for documentation')
  provides=('libwireplumber-0.4.so' 'pipewire-session-manager')
  conflicts=('pipewire-media-session')
  install="${pkgname}.install"

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {NEWS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove files provided by wireplumber-docs
  rm -frv "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_wireplumber-docs() {
  pkgdesc+=" (documentation)"

  cd "$pkgbase-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # remove files provided by wireplumber
  rm -frv "${pkgdir}/usr/"{bin,include,lib,share/{gir*,wireplumber}} "${pkgdir}/etc/"
}
