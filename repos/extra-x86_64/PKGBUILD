# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=(pangomm pangomm-docs)
pkgver=2.46.1
pkgrel=1
pkgdesc="C++ bindings for Pango"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(pango glibmm cairomm)
makedepends=(git mm-common glibmm-docs cairomm-docs meson)
options=(!emptydirs)
_commit=2ed5891a274b65e0ab1cf2effe556d3963870040  # tags/2.46.1^0
source=("git+https://gitlab.gnome.org/GNOME/pangomm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  arch-meson $pkgbase build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pangomm() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_pangomm-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
