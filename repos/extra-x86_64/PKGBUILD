# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=40.0
pkgrel=2
pkgdesc="View current processes and monitor system state"
url="https://wiki.gnome.org/Apps/SystemMonitor"
arch=(x86_64)
license=(GPL)
depends=(libgtop gtkmm3 librsvg systemd libhandy polkit)
makedepends=(meson yelp-tools git appstream-glib)
groups=(gnome)
_commit=24e810d54e21d7be34be9807d025f77ef0295193  # tags/40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-system-monitor.git#commit=$_commit"
        force-hdybar.diff)
sha256sums=('SKIP'
            '411e619160597ea86bbe29ff2e8f8830aa0f51cadbed45dac03c3301a92e4644')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/70120
  # https://gitlab.gnome.org/GNOME/gnome-system-monitor/-/issues/182
  git apply -3 ../force-hdybar.diff
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
