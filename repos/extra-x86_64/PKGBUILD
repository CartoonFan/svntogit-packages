# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tinywrkb <tinywrkb@gmail.com>

pkgname=power-profiles-daemon
pkgver=0.10.0
pkgrel=2
pkgdesc='Makes power profiles handling available over D-Bus'
url='https://gitlab.freedesktop.org/hadess/power-profiles-daemon'
license=(GPL3)
arch=(x86_64)
depends=(upower polkit)
optdepends=('python-gobject: for powerprofilesctl')
makedepends=(meson)
checkdepends=(python-dbusmock python-isort python-mccabe umockdev)
source=(https://gitlab.freedesktop.org/hadess/power-profiles-daemon/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('e8c92bb4e848eca3c0b5263c5e4ae0c4aea51ce608086263148efd65d7520815b1a8efe2876c78fbff13a0c503a5fb9426fa70e050c8b3fc7943da0e7068a41f')

build() {
  meson $pkgname-$pkgver build \
    --prefix /usr \
    --libexec lib
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
