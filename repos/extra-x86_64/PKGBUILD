# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kglobalaccel
pkgver=5.80.0
pkgrel=2
pkgdesc='Add support for global workspace shortcuts'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdbusaddons kconfig kcrash)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
         kglobalaccel-check-user.patch::"https://invent.kde.org/frameworks/kglobalaccel/-/commit/bbec7433.patch")
sha256sums=('36f0abd93064b1b213c87d2e182649bc2f43cdd78ab45a4d686d9fb2c609bd04'
            'SKIP'
            'e42a8b9a7aa27d199855adfac1cc554d329e579375af13a95bafa9deec522b85')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB  # David Faure <faure@kde.org>
              2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell

prepare() {
  patch -d $pkgname-$pkgver -p1 < kglobalaccel-check-user.patch # Prevent running daemon as a different user
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
