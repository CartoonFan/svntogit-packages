# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <anton@hvornum.se>
# Contributor: demostanis worlds <demostanis@protonmail.com>

pkgbase=archinstall
pkgname=('archinstall' 'python-archinstall')
pkgver=2.1.3
pkgrel=1
pkgdesc="Just another guided/automated Arch Linux installer with a twist"
arch=('any')
url="https://github.com/Torxed/archinstall"
license=('GPL')
depends=('python')
makedepends=('python-pip' 'python-setuptools' 'python-sphinx')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz.asc")
md5sums=('3ef628d005a74c1e8004bcde87b034b6'
         'SKIP')
sha512sums=('f37aa483c29f71c44135d3ff2a910ae101d8be5e70e4ed6af2038d41df2301d43ec9d1024ce1cab23a58828e279f1c8256ea9ece7ca98eb04f0719c6ec941399'
            'SKIP')
validpgpkeys=('256F73CEEFC6705C6BBAB20E5FBBB32941E3740A') # Anton Hvornum (Torxed) <anton@hvornum.se>


build() {
	cd "$pkgbase-$pkgver"

        python setup.py build

        cd docs
        make man
}


package_archinstall() {
        depends=('python-archinstall')
	cd "$pkgbase-$pkgver"

        mkdir -p "${pkgdir}/usr/bin"
        
        # Install a guided profile
	cat - > "${pkgdir}/usr/bin/archinstall" <<EOF
#!/bin/sh
python -m archinstall $@
EOF

	chmod +x "${pkgdir}/usr/bin/archinstall"

        install -Dm644 docs/_build/man/archinstall.1 "${pkgdir}"/usr/share/man/man1/archinstall.1
}

package_python-archinstall() {
	cd "$pkgbase-$pkgver"

        python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
