# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=40rc
pkgrel=1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
makedepends=(glib2 meson)
license=(GPL)
groups=(gnome)
_tarver=${pkgver/rc/.rc}
source=(https://download.gnome.org/sources/$pkgname/${_tarver%%.*}/$pkgname-$_tarver.tar.xz)
sha256sums=('8d28dacbf17e8fc5cad030ce027398befbfb2c0034ae14a8e6a7af3336fda679')

build() {
  arch-meson $pkgname-$_tarver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
