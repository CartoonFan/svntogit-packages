# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-server
pkgver=0.7.1
pkgrel=1
pkgdesc="Library to discover, browse and manipulate Digital Media Servers"
url="https://github.com/phako/dleyna-server"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git meson)
_commit=fc44af8669ca81a662c9018228081f1b8f0b641d  # tags/v0.7.1^0
source=("git+https://github.com/phako/dleyna-server#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Add missing soup_session_new call
  git cherry-pick -n afc2a04c20d7722b7278e1b982452b71c9bd6da1

  # Fix parsing of empty results
  git cherry-pick -n 5c6efbbf597fe9bc44ba4ef4aadd7a98c8262241
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
