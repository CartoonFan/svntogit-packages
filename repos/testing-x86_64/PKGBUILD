# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.4.2
pkgrel=3
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
provides=('libffi.so')
source=(https://github.com/libffi/libffi/releases/download/v$pkgver/libffi-$pkgver.tar.gz
        https://github.com/libffi/libffi/releases/download/v3.3/libffi-3.3.tar.gz)
sha256sums=('540fb721619a6aba3bdeef7d940d8e9e0e6d2c193595bc243241b77ff9e93620'
            '72fba7922703ddfa7a028d513ac15a85c8d54c8d67f55fa5a4802885dc652056')
b2sums=('a8137bc895b819f949fd7705e405be627219c6d1fdef280253330f7407d4a548bb057d7bb0e9225d1767d42f9bf5f0ab3c455db1c3470d7cc876bb7b7d55d308'
        'cddc40729a30a9bd34d675809f51f8d1b4ccaffa54bc6dd6f7e965f4e260edd34754719f9f6247c8957aeb7cf154d56ce1fe16a54c3f1ad39afbebdf41d23caa')

build() {
  # TODO: remove this block once the libffi 3.4 rebuilds are bootstrapped
  pushd $pkgname-3.3
  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-pax_emutramp
  make
  popd

  cd $pkgname-$pkgver
  # remove --disable-exec-static-tramp once ghc and gobject-introspection
  # work fine with it enabled (https://github.com/libffi/libffi/pull/647)
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-multi-os-directory \
    --disable-exec-static-tramp \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  # TODO: remove this block once the libffi 3.4 rebuilds are bootstrapped
  pushd $pkgname-3.3
  make DESTDIR="$srcdir"/fakeinstall install
  install -Dt "$pkgdir"/usr/lib "$srcdir"/fakeinstall/usr/lib/libffi.so.7
  popd

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}

# vim: ts=2 sw=2 et:
