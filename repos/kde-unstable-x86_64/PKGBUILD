# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail-account-wizard
pkgver=21.07.80
pkgrel=1
pkgdesc='KMail account wizard'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL2)
depends=(kidentitymanagement pimcommon kmailtransport kross)
makedepends=(extra-cmake-modules boost kdoctools qt5-tools knotifyconfig ktexteditor)
groups=(kde-applications kdepim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('09a15d83c63a49ec540141b1dfd607ca97aef40f8a2f86a70ac9855c41acb581'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
