# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khelpcenter
pkgver=21.03.80
pkgrel=1
pkgdesc="Application to show KDE Applications' documentation"
arch=(x86_64)
url='https://userbase.kde.org/KHelpCenter'
license=(LGPL)
groups=(kde-applications kde-system)
depends=(khtml kdoctools xapian-core grantlee kinit perl)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e424e60e3aa43d1cfccde6a04da0a1788f6690345aad7a6ab9872d58cc81740c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
