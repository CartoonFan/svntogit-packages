# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmp4v2
pkgver=5.0.0
pkgrel=1
pkgdesc="An API to create and modify mp4 files as defined by ISO-IEC:14496-1:2001 MPEG-4 Systems"
arch=('x86_64')
license=('MPL')
url="https://github.com/TechSmith/mp4v2"
depends=('gcc-libs')
source=("https://github.com/TechSmith/mp4v2/archive/Release-ThirdParty-MP4v2-$pkgver.tar.gz")
sha512sums=('1fed7d6e812a2f2aa681556b923e2abdbf3086d2334d875f0fd23ed792469e9c62596aa9fa5772c0f18d56c26a14cc84a2e2d3368f6447cf0b613aedf492025e')

build() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver

  # https://github.com/TechSmith/mp4v2/issues/48
  CXXFLAGS="$CXXFLAGS -Wno-narrowing" ./configure --prefix=/usr
  make
}

package() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-man
}
