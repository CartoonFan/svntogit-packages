# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-csv23
pkgver=0.3.2
pkgrel=6
pkgdesc="Python 3 unicode CSV compatibility layer"
url="https://github.com/xflr6/csv23"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pytest-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xflr6/csv23/archive/v$pkgver.tar.gz"
        $pkgname-python310.patch::https://github.com/xflr6/csv23/compare/v0.3.2..9dfb3389bd8d.patch)
sha512sums=('289386cce5de4ae5bc42fc56d1639bbd1483034e5422f63142c1d188de735b1f5f18f9f69e3cb96f4ac3e1d7d0b371b1c45e720982c4f4b539fcf0d2da893f6a'
            '443422d055cf6a2a1ec5316fa33c13caaf9269902958ad2b43e6976f7591bb578597a6f3d6283e1ec760bb552698256d3eb9a3ca76c0c15591b20cca786d11e7')

prepare() {
  cd csv23-$pkgver
  patch -Np1 -i ../$pkgname-python310.patch || :
  sed -i '/--cov/d' setup.cfg
}

build() {
  cd csv23-$pkgver
  python setup.py build
}

check() {
  cd csv23-$pkgver
  python -m pytest
}

package() {
  cd csv23-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
