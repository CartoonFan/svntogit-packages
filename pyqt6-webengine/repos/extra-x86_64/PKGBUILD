# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-webengine
pkgname=(python-pyqt6-webengine)
pkgver=6.2.0
pkgrel=1
pkgdesc='Python bindings for QtWebEngine'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtwebengine/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-webengine python-pyqt6)
makedepends=(sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-WebEngine/PyQt6_WebEngine-$pkgver.tar.gz)
sha256sums=('4f12a984efd01d202a89baea3437c6fb2001a042f9bdef512d324eb4e81ef693')

build() {
  cd PyQt6_WebEngine-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6 \
    --concatenate 1
  cd build
  make
}

package_python-pyqt6-webengine() {
  cd PyQt6_WebEngine-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
