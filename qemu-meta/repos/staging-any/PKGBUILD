# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=qemu-meta
pkgname=(qemu-{base,desktop,emulators-full,full})
pkgver=1
pkgrel=1
pkgdesc="A generic and open source machine emulator and virtualizer"
arch=(any)
url="https://www.qemu.org/"
license=(GPL2 LGPL2.1)

package_qemu-base() {
  pkgdesc="A basic QEMU setup for headless environments"
  depends=(
    qemu
    qemu-audio-spice
    qemu-block-{curl,dmg,nfs,ssh}
    qemu-chardev-spice
    qemu-hw-display-{qxl,virtio-gpu{,-{gl,pci,pci-gl}}}
    qemu-hw-display-virtio-vga{,-gl}
    qemu-hw-s390x-virtio-gpu-ccw
    qemu-hw-usb-{host,redirect,smartcard}
    qemu-img
    qemu-pr-helper
    qemu-system-x86
    qemu-tools
    qemu-ui-{curses,spice-{app,core}}
    qemu-virtiofsd
  )
  # TODO: remove conflicts/provides/replaces after 2022-10-01
  conflicts=(qemu-headless)
  provides=(qemu-headless)
  replaces=(qemu-headless)
}

package_qemu-desktop() {
  pkgdesc="A QEMU setup for desktop environments"
  depends=(
    qemu
    qemu-audio-{alsa,dbus,jack,oss,pa,sdl,spice}
    qemu-block-{curl,dmg,nfs,ssh}
    qemu-chardev-spice
    qemu-hw-display-{qxl,virtio-gpu{,-{gl,pci,pci-gl}}}
    qemu-hw-display-virtio-vga{,-gl}
    qemu-hw-s390x-virtio-gpu-ccw
    qemu-hw-usb-{host,redirect,smartcard}
    qemu-img
    qemu-pr-helper
    qemu-system-x86
    qemu-tools
    qemu-ui-{curses,dbus,egl-headless,gtk,opengl,sdl,spice-{app,core}}
    qemu-vhost-user
    qemu-virtiofsd
  )
}

package_qemu-emulators-full() {
  pkgdesc="All QEMU user mode and system emulators"
  depends=(
    qemu-system-{aarch64,alpha,arm,avr,cris,hppa,m68k,microblaze,mips,nios2,or1k,ppc,riscv,rx,s390x,sh4,sparc,tricore,x86,xtensa}
    qemu-user
  )
  # TODO: remove conflicts/provides/replaces after 2022-10-01
  conflicts=(qemu-arch-extra qemu-headless-arch-extra)
  provides=(qemu-arch-extra qemu-headless-arch-extra)
  replaces=(qemu-arch-extra qemu-headless-arch-extra)
}

package_qemu-full() {
  pkgdesc="A full QEMU setup"
  depends=(
    qemu
    qemu-audio-{alsa,dbus,jack,oss,pa,sdl,spice}
    qemu-block-{curl,dmg,gluster,iscsi,nfs,rbd,ssh}
    qemu-chardev-{baum,spice}
    qemu-docs
    qemu-guest-agent
    qemu-hw-display-{qxl,virtio-{gpu{,-{gl,pci,pci-gl}},vga{,-gl}}}
    qemu-hw-s390x-virtio-gpu-ccw
    qemu-hw-usb-{host,redirect,smartcard}
    qemu-img
    qemu-pr-helper
    qemu-system-{aarch64,alpha,arm,avr,cris,hppa,m68k,microblaze,mips,nios2,or1k,ppc,riscv,rx,s390x,sh4,sparc,tricore,x86,xtensa}
    qemu-tests
    qemu-tools
    qemu-ui-{curses,dbus,egl-headless,gtk,opengl,sdl,spice-{app,core}}
    qemu-user
    qemu-vhost-user-gpu
    qemu-virtiofsd
  )
}

