# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coordgen
pkgver=2.0.2
pkgrel=1
pkgdesc="Schrodinger-developed 2D Coordinate Generation"
arch=(x86_64)
url="https://github.com/schrodinger/coordgenlibs"
license=(BSD)
depends=(maeparser)
makedepends=(cmake boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/schrodinger/coordgenlibs/archive/v$pkgver.tar.gz")
sha256sums=('eb7d2fa769ab09766b179237a2dcc7a93cfeba94bac5d96af588a22225f6ec02')

build() {
  cmake -B build -S coordgenlibs-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOORDGEN_USE_MAEPARSER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 coordgenlibs-$pkgver/templates.mae -t "$pkgdir"/usr/share/coordgen
  install -Dm644 coordgenlibs-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
